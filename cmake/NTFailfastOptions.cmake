if(DEFINED _NT_FAILFAST_OPTIONS_INCLUDED)
    return()
endif()
set(_NT_FAILFAST_OPTIONS_INCLUDED ON)

function(nt_failfast_cache_bool var default doc)
    if(NOT DEFINED ${var})
        set(${var} ${default} CACHE BOOL "${doc}" FORCE)
    else()
        set(${var} ${${var}} CACHE BOOL "${doc}" FORCE)
    endif()
endfunction()

function(nt_failfast_configure_warning_flags default_flags doc)
    if(NOT DEFINED NT_FAILFAST_WARNING_FLAGS)
        set(NT_FAILFAST_WARNING_FLAGS "${default_flags}" CACHE STRING "${doc}" FORCE)
    else()
        set(NT_FAILFAST_WARNING_FLAGS "${NT_FAILFAST_WARNING_FLAGS}" CACHE STRING "${doc}" FORCE)
    endif()
endfunction()

function(nt_failfast_configure_link_flags debug_flags release_flags doc)
    if(NOT DEFINED NT_FAILFAST_LINK_FLAGS_DEBUG)
        set(NT_FAILFAST_LINK_FLAGS_DEBUG "${debug_flags}" CACHE STRING "${doc} (Debug)" FORCE)
    endif()
    if(NOT DEFINED NT_FAILFAST_LINK_FLAGS_RELEASE)
        set(NT_FAILFAST_LINK_FLAGS_RELEASE "${release_flags}" CACHE STRING "${doc} (Release)" FORCE)
    endif()

    set(_nt_link_flags_active "${NT_FAILFAST_LINK_FLAGS_DEBUG}")
    if(CMAKE_BUILD_TYPE)
        if(CMAKE_BUILD_TYPE MATCHES "^(Release|RelWithDebInfo|MinSizeRel)$")
            set(_nt_link_flags_active "${NT_FAILFAST_LINK_FLAGS_RELEASE}")
        endif()
    endif()

    set(NT_FAILFAST_LINK_FLAGS "${_nt_link_flags_active}" CACHE STRING "${doc}" FORCE)
endfunction()
