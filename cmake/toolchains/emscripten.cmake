# emscripten.cmake

set(CMAKE_SYSTEM_NAME Emscripten)
set(CMAKE_SYSTEM_PROCESSOR wasm32)

set(EMSDK_PATH "$ENV{EMSDK}")
if(NOT EMSDK_PATH)
    message(FATAL_ERROR "EMSDK env var is not set. Activate emsdk (emsdk_env) before configuring.")
endif()

set(EMSCRIPTEN_ROOT "${EMSDK_PATH}/upstream/emscripten" CACHE PATH "Path to Emscripten root")

if(WIN32)
    set(_EMCC_PATH "${EMSCRIPTEN_ROOT}/emcc.bat")
    set(_EMXX_PATH "${EMSCRIPTEN_ROOT}/em++.bat")
else()
    set(_EMCC_PATH "${EMSCRIPTEN_ROOT}/emcc")
    set(_EMXX_PATH "${EMSCRIPTEN_ROOT}/em++")
endif()

if(NOT EXISTS "${_EMCC_PATH}")
    message(FATAL_ERROR "emcc not found at ${_EMCC_PATH}")
endif()
if(NOT EXISTS "${_EMXX_PATH}")
    message(FATAL_ERROR "em++ not found at ${_EMXX_PATH}")
endif()

set(CMAKE_C_COMPILER   "${_EMCC_PATH}")
set(CMAKE_CXX_COMPILER "${_EMXX_PATH}")
set(CMAKE_FIND_ROOT_PATH "${EMSCRIPTEN_ROOT}")
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)

include("${CMAKE_CURRENT_LIST_DIR}/../NTFailfastOptions.cmake")

set(_NT_SANITIZER_DEFAULT ON)
if(CMAKE_BUILD_TYPE STREQUAL "Release")
    set(_NT_SANITIZER_DEFAULT OFF)
endif()
nt_failfast_cache_bool(NT_FAILFAST_SANITIZER ${_NT_SANITIZER_DEFAULT} "Enable fail-fast sanitizer instrumentation")

nt_failfast_cache_bool(NT_FAILFAST_LINK_ENFORCE ON "Force fail-fast linker guard flags")

if(NOT DEFINED NT_FAILFAST_LINK_FLAGS)
    set(NT_FAILFAST_LINK_FLAGS "-sERROR_ON_UNDEFINED_SYMBOLS=1;-sSTACK_OVERFLOW_CHECK=2;-sSAFE_HEAP=1" CACHE STRING "Fail-fast linker guard flags for Emscripten targets" FORCE)
endif()

set(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/sandbox")

set(WEB_SOURCE_MAP_BASE "" CACHE STRING "Source-map base URL, e.g. http://localhost:8000/")
set(WEB_EMIT_SOURCE_MAPS_DEBUG ON CACHE BOOL "Emit WebAssembly source maps when building Debug")
set(WEB_EMIT_SOURCE_MAPS_RELEASE OFF CACHE BOOL "Emit WebAssembly source maps when building Release")

set(_NT_EMIT_SOURCE_MAPS_DEFAULT OFF)
if(CMAKE_BUILD_TYPE STREQUAL "Debug")
    set(_NT_EMIT_SOURCE_MAPS_DEFAULT ${WEB_EMIT_SOURCE_MAPS_DEBUG})
elseif(CMAKE_BUILD_TYPE STREQUAL "Release")
    set(_NT_EMIT_SOURCE_MAPS_DEFAULT ${WEB_EMIT_SOURCE_MAPS_RELEASE})
endif()

set(_NT_SOURCE_MAP_FLAGS "")
if(_NT_EMIT_SOURCE_MAPS_DEFAULT)
    set(_NT_SOURCE_MAP_FLAGS "-gsource-map")
    if(WEB_SOURCE_MAP_BASE)
        string(APPEND _NT_SOURCE_MAP_FLAGS " --source-map-base=${WEB_SOURCE_MAP_BASE}")
    endif()
endif()

set(NT_WASM_EMIT_SOURCE_MAPS ${_NT_EMIT_SOURCE_MAPS_DEFAULT} CACHE INTERNAL "Whether the active wasm build emits source maps")

set(CMAKE_C_FLAGS_DEBUG           "-O0 -g" CACHE STRING "" FORCE)
set(CMAKE_CXX_FLAGS_DEBUG         "-O0 -g" CACHE STRING "" FORCE)
set(_NT_DEBUG_LINK_FLAGS          "-g -sASSERTIONS=2 -sSAFE_HEAP=1 -sSTACK_OVERFLOW_CHECK=2 -sDEMANGLE_SUPPORT=1 --profiling-funcs")
if(_NT_SOURCE_MAP_FLAGS)
    string(APPEND _NT_DEBUG_LINK_FLAGS " ${_NT_SOURCE_MAP_FLAGS}")
endif()
set(CMAKE_EXE_LINKER_FLAGS_DEBUG  "${_NT_DEBUG_LINK_FLAGS}" CACHE STRING "" FORCE)

set(_NT_RELEASE_C_FLAGS "-Oz -flto")
set(_NT_RELEASE_CXX_FLAGS "-Oz -flto")
if(_NT_EMIT_SOURCE_MAPS_DEFAULT)
    string(APPEND _NT_RELEASE_C_FLAGS " -g")
    string(APPEND _NT_RELEASE_CXX_FLAGS " -g")
endif()
set(CMAKE_C_FLAGS_RELEASE   "${_NT_RELEASE_C_FLAGS}" CACHE STRING "" FORCE)
set(CMAKE_CXX_FLAGS_RELEASE "${_NT_RELEASE_CXX_FLAGS}" CACHE STRING "" FORCE)

set(_NT_RELEASE_LINK_FLAGS "-sASSERTIONS=0 -sERROR_ON_UNDEFINED_SYMBOLS=1 -sSTRICT=1")
if(_NT_SOURCE_MAP_FLAGS)
    string(APPEND _NT_RELEASE_LINK_FLAGS " ${_NT_SOURCE_MAP_FLAGS}")
endif()
set(CMAKE_EXE_LINKER_FLAGS_RELEASE "${_NT_RELEASE_LINK_FLAGS}" CACHE STRING "" FORCE)

if(NT_FAILFAST_SANITIZER)
    set(_NT_SANITIZER_FLAGS "-fsanitize=address,undefined -fno-omit-frame-pointer")
    set(_NT_SANITIZER_LINK_FLAGS "-fsanitize=address,undefined")
    foreach(_conf DEBUG RELEASE)
        set(CMAKE_C_FLAGS_${_conf} "${CMAKE_C_FLAGS_${_conf}} ${_NT_SANITIZER_FLAGS}" CACHE STRING "" FORCE)
        set(CMAKE_CXX_FLAGS_${_conf} "${CMAKE_CXX_FLAGS_${_conf}} ${_NT_SANITIZER_FLAGS}" CACHE STRING "" FORCE)
        set(CMAKE_EXE_LINKER_FLAGS_${_conf} "${CMAKE_EXE_LINKER_FLAGS_${_conf}} ${_NT_SANITIZER_LINK_FLAGS}" CACHE STRING "" FORCE)
    endforeach()
endif()

if(NT_FAILFAST_LINK_ENFORCE)
    string(REPLACE ";" " " _nt_link_guard_str "${NT_FAILFAST_LINK_FLAGS}")
    foreach(_conf DEBUG RELEASE)
        set(CMAKE_EXE_LINKER_FLAGS_${_conf} "${CMAKE_EXE_LINKER_FLAGS_${_conf}} ${_nt_link_guard_str}" CACHE STRING "" FORCE)
    endforeach()
endif()
